/*************************************
exportratio.wresl
 
Nazrul Islam DWR (04/27/2010)

Export-Inflow Ratio restriction on exports
**************************************/

! Delta Export defined as in DWRSIM Algorithm Description for Export Ratio
define ExportActual {alias  D_Jones+D_Banks KIND 'EXPORT-PRJ' units 'CFS'  }

! Delta Inflow defined as in DWRSIM Algorithm Description for Export Ratio
define Inflow {alias C_Hood_ANN+AD_Mokelumne+C_YoloBP+AD_SJR+AD_Calaveras+I_MarshCr kind 'INFLOW-DELTA' UNITS 'CFS'   }

! EI allowable export variable - MAXIMUM ALLOWABLE EXPORT due to EXPORT RATIO
define EiExpCtrl {std kind 'EXPORT-CTRL-EI' units 'CFS'}

! EI Ratio dependent on month
define ExpRatio {
    case feb    {
        condition month == FEB
        select  ratio
        from    febeiratio
        where   wateryear=wateryear}
    case rest   {
        condition always
          select    ratio
          from      eiratio
          where     month=month}
}

! Write out EI ratio
define ExpRatio_ {alias ExpRatio KIND 'EI-RATIO-STD' UNITS 'NONE'}

! Compute exports allowable by the EI ratio
goal find_max_export {EiExpCtrl = ExpRatio*Inflow}

! Restrict exports to be less than that allowable by EI ratio
goal export_comply { ExportActual < EiExpCtrl }
